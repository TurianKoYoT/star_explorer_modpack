giga_shroud_conduit_worker = {
    category = specialist
    condition_string = SPECIALIST_JOB_TRIGGER
    building_icon = building_giga_shroud_conduit
    clothes_texture_index = 3
    possible_pre_triggers = {
        has_owner = yes
        is_being_purged = no
        is_being_assimilated = no
        is_sapient = yes
    }
    possible_precalc = can_fill_specialist_job
    possible = {
        complex_specialist_job_check_trigger = yes
        has_psionic_species_trait = yes
    }
    inline_script = {
        script = jobs/giga_shroud_conduit_job_resources_effects
        category = planet_administrators
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                has_valid_civic = civic_byzantine_bureaucracy
            }
        }
        modifier = {
            planet_stability_add = 1
        }
    }
    triggered_planet_modifier = {
        potential = {
            planet = {
                has_active_building = building_league_offices
            }
        }
        planet_amenities_add = 1
        mult = value:scripted_modifier_mult|MODIFIER|pop_job_amenities_mult|
    }
    triggered_country_modifier = {
        potential = {
            exists = owner
            owner = {
                has_valid_civic = civic_efficient_bureaucracy
            }
        }
        country_edict_fund_add = 2
        mult = value:job_total_output_modifier|JOB|bureaucrat|RESOURCE|unity|
    }
    weight = {
        weight = value:giga_specialist_weight|JOB|giga_shroud_conduit_worker|RESOURCE|unity|INDENTURED|1.2|
    }
}
