district_giga_voidframe_sanctuary_dm = {
    base_buildtime = 480
    icon = district_giga_voidframe_admin_dm
    is_capped_by_modifier = no
    show_on_uncolonized = {
        exists = from
        uses_district_set = giga_frameworld
        from = {
            is_machine_empire = yes
            has_valid_civic = civic_machine_servitor
        }
        is_voidframe_exclusive_dm = yes
    }
    potential = {
        exists = owner
        uses_district_set = giga_frameworld
        owner = {
            is_machine_empire = yes
            has_valid_civic = civic_machine_servitor
        }
        if = {
            limit = {
                is_voidframe_outdated_dm = yes
            }
            has_building = building_dm_ancient_cryo_chamber
            NOR = {
                has_building = building_ae_ancient_cryo_chamber
                has_building = building_phanon_ancient_cryo_chamber
                has_building = building_stellarite_ancient_cryo_chamber
            }
        }
        else = {
            is_voidframe_dm = yes
            OR = {
                has_building = building_dm_ancient_cryo_chamber
                has_building = building_ae_ancient_cryo_chamber
                has_building = building_phanon_ancient_cryo_chamber
                has_building = building_stellarite_ancient_cryo_chamber
            }
        }
    }
    prerequisites = {
        tech_dm_ancient_cryo_chamber
    }
    show_tech_unlock_if = {
        giga_has_frameworld_origin = yes
        owner = {
            is_machine_empire = yes
            has_valid_civic = civic_machine_servitor
        }
    }
    conversion_ratio = 1
    convert_to = {
        district_giga_frameworld_administrative
        district_giga_voidframe_admin_dm
        district_giga_voidframe_admin_de
        district_giga_voidframe_admin_pe
        district_giga_voidframe_admin_se
        district_giga_frameworld_temple
        district_giga_voidframe_temple_dm
        district_giga_voidframe_temple_de
        district_giga_voidframe_temple_pe
        district_giga_voidframe_temple_se
        district_giga_frameworld_simulator
        district_giga_voidframe_simulation_dm
        district_giga_voidframe_simulation_de
        district_giga_voidframe_simulation_pe
        district_giga_voidframe_simulation_se
        district_giga_frameworld_synapse
        district_giga_voidframe_synapse_dm
        district_giga_voidframe_synapse_de
        district_giga_voidframe_synapse_pe
        district_giga_voidframe_synapse_se
        district_giga_frameworld_sanctuary
        district_giga_voidframe_sanctuary_dm
        district_giga_voidframe_sanctuary_de
        district_giga_voidframe_sanctuary_pe
        district_giga_voidframe_sanctuary_se
    }
    resources = {
        category = planet_districts_hab_research
        cost = {
            minerals = 1000
            sr_dark_matter = 200
        }
        upkeep = {
            energy = 4
        }
    }
    planet_modifier = {
        planet_housing_add = $house$
        job_bio_trophy_add = 25
    }
    triggered_planet_modifier = {
        potential = {
            has_planet_flag = frameworld_advanced_sanctuary
        }
        planet_housing_add = $house$
        job_bio_trophy_add = 25
    }
    triggered_desc = {
        text = voidframe_sanctuary_biotrophy_scaling
    }
    ai_resource_production = {
        unity = 4
        trigger = {
            always = yes
        }
    }
    ai_resource_production = {
        trigger = {
            free_district_slots > 0
            has_unemployed_pop_of_category = bio_trophy
        }
        ai_unobtainium = 1000
    }
    ai_resource_production = {
        unity = 40
        physics_research = 40
        trigger = {
            has_unemployed_pop_of_category = bio_trophy
            planet_resource_compare = {
                resource = physics_research
                value >= 100
                type = produces
            }
        }
    }
    ai_resource_production = {
        unity = 40
        alloys = 40
        trigger = {
            has_unemployed_pop_of_category = bio_trophy
            planet_resource_compare = {
                resource = alloys
                value >= 100
                type = produces
            }
        }
    }
    ai_resource_production = {
        unity = 40
        consumer_goods = 40
        trigger = {
            has_unemployed_pop_of_category = bio_trophy
            planet_resource_compare = {
                resource = consumer_goods
                value >= 100
                type = produces
            }
        }
    }
}
