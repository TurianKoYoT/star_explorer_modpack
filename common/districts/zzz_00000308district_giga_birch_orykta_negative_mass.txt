@giga_birch_sector_cost = 1500
@giga_birch_sector_alt_cost = 500
@giga_birch_sector_time = 360
@giga_birch_sector_maintenance = 5
@giga_birch_sector_alt_maintenance = 5
@sector_jobs = 7
@sector_workers = -7
district_giga_birch_orykta_negative_mass = {
    base_buildtime = @giga_birch_sector_time
    is_capped_by_modifier = yes
    show_on_uncolonized = {
        uses_district_set = giga_birch
        has_district = district_giga_birch_orykta
        not = {
            has_global_flag = ehof_disabled
        }
        exists = from
        from = {
            has_technology = tech_nm_utilization_1
        }
    }
    potential = {
        uses_district_set = giga_birch
        has_district = district_giga_birch_orykta
        not = {
            has_global_flag = ehof_disabled
        }
        exists = owner
        owner = {
            has_technology = tech_nm_utilization_1
        }
    }
    allow = {
        giga_birch_district_allow = {
            category = orykta
        }
    }
    resources = {
        category = planet_districts
        cost = {
            minerals = @giga_birch_sector_cost
            alloys = @giga_birch_sector_alt_cost
        }
        upkeep = {
            energy = @giga_birch_sector_maintenance
            alloys = @giga_birch_sector_alt_maintenance
        }
    }
    planet_modifier = {
        giga_birch_district_orykta_tag = 1
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                is_gestalt = no
            }
        }
        job_ehof_energy_converter_add = @sector_jobs
        job_giga_birch_orykta_worker_add = @sector_workers
    }
    triggered_desc = {
        trigger = {
            exists = owner
            owner = {
                is_gestalt = no
            }
        }
        text = job_ehof_energy_converter_effect_desc
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                is_gestalt = yes
            }
        }
        job_ehof_energy_converter_drone_add = @sector_jobs
        job_giga_birch_orykta_worker_drone_add = @sector_workers
    }
    triggered_desc = {
        trigger = {
            exists = owner
            owner = {
                is_gestalt = yes
            }
        }
        text = job_ehof_energy_converter_drone_effect_desc
    }
    on_queued = {
        giga_birch_district_queued = {
            category = orykta
            type = orykta_negative_mass
        }
    }
    on_unqueued = {
        giga_birch_district_unqueued = {
            category = orykta
            type = orykta_negative_mass
        }
    }
    on_built = {
        giga_birch_district_built = {
            category = orykta
            type = orykta_negative_mass
        }
    }
    on_destroy = {
        giga_birch_district_destroy = {
            category = orykta
            type = orykta_negative_mass
        }
    }
}
