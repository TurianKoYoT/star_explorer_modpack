@resolution_weight_loved = 7
@resolution_cost_t1 = 100
@resolution_flag_timer = 360
resolution_commerce_buzzword_standardization = {
    icon = "GFX_resolution_galactic_commerce"
    resources = {
        category = resolutions
        cost = {
            influence = @resolution_cost_t1
        }
    }
    target = no
    level = 1
    modifier = {
        resolution_commerce_buzzword_standardization = 1
    }
    effect = {
        hidden_effect = {
            set_global_flag = galactic_community_resolution_passed
            set_timed_country_flag = {
                flag = galactic_community_resolution_passed_commerce
                days = @resolution_flag_timer
            }
            add_modifier = {
                modifier = resolution_passed_diplomatic_weight
                days = 2200
            }
        }
    }
    fail_effects = {
        hidden_effect = {
            set_timed_country_flag = {
                flag = galactic_community_resolution_failed_commerce
                days = @resolution_flag_timer
            }
            add_modifier = {
                modifier = resolution_failed_diplomatic_weight
                days = 2200
            }
        }
    }
    allow = {
        custom_tooltip = {
            fail_text = "resolution_already_active"
            NOR = {
                is_active_resolution = "resolution_commerce_buzzword_standardization"
                is_active_resolution = "resolution_commerce_leveraged_privateering"
                is_active_resolution = "resolution_commerce_underdeveloped_system_utilization"
                is_active_resolution = "resolution_commerce_holistic_asset_coordination"
                is_active_resolution = "resolution_commerce_profit_maximization_engines"
                is_active_resolution = "resolution_commerce_6"
            }
        }
    }
    ai_weight = {
        base = @resolution_weight_loved
        modifier = {
            factor = 2
            is_capitalist = yes
            desc = auth_corporate
        }
        modifier = {
            factor = 1.2
            is_civic_free_traders = yes
            desc = gal_com_trade_civic
        }
        modifier = {
            factor = 0.75
            is_anti_corp = yes
            desc = gal_com_anti_corp_civic
        }
        modifier = {
            factor = 0.75
            has_valid_civic = civic_byzantine_bureaucracy
            desc = civic_byzantine_bureaucracy
        }
        modifier = {
            factor = 1.25
            is_civic_efficient_bureaucracy = yes
            desc = gal_com_efficient_civic
        }
        modifier = {
            factor = 0.25
            is_shared_burden_empire = yes
            desc = civic_shared_burden
        }
        modifier = {
            factor = 0.5
            is_despoiler_empire = yes
            desc = civic_barbaric_despoilers
        }
        modifier = {
            factor = 0.1
            is_gestalt = yes
            desc = ethic_gestalt_consciousness
        }
    }
}